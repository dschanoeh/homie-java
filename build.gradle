plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "com.diffplug.spotless" version "6.9.1"
    id "jacoco"
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

group = 'io.github.dschanoeh'
version = '0.2.0'
sourceCompatibility = '1.8'

jacocoTestReport {
    dependsOn test
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

shadowJar {
    archiveVersion = project.getVersion()
    archiveClassifier = ''
    archiveBaseName = 'homie-java'
}

spotless {
    java {
        indentWithSpaces()
        trimTrailingWhitespace()
    }
}

publishing {
    publications {
        homieJava(MavenPublication){
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/dschanoeh/homie-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
