plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.diffplug.spotless" version "5.11.0"
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.3'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
}

group = 'io.github.dschanoeh'
version = '0.1.2'
sourceCompatibility = '1.8'

test {
    useJUnitPlatform()
}

shadowJar {
    archiveVersion = project.getVersion()
    archiveClassifier = ''
    archiveBaseName = 'homie-java'
}

spotless {
    java {
        indentWithSpaces()
        trimTrailingWhitespace()
    }
}

publishing {
    publications {
        homieJava(MavenPublication){
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/dschanoeh/homie-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
